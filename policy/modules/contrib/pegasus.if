## <summary>The Open Group Pegasus CIM/WBEM Server.</summary>

########################################
## <summary>
##	All of the rules required to
##	administrate an pegasus environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`pegasus_admin',`
	gen_require(`
		type pegasus_t, pegasus_initrc_exec_t, pegasus_tmp_t;
		type pegasus_cache_t, pegasus_data_t, pegasus_conf_t;
		type pegasus_mof_t, pegasus_var_run_t;
	')

	allow $1 pegasus_t:process { ptrace signal_perms };
	ps_process_pattern($1, pegasus_t)

	init_labeled_script_domtrans($1, pegasus_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 pegasus_initrc_exec_t system_r;
	allow $2 system_r;

	files_search_etc($1)
	admin_pattern($1, pegasus_conf_t)

	files_search_usr($1)
	admin_pattern($1, pegasus_mof_t)

	files_search_tmp($1)
	admin_pattern($1, pegasus_tmp_t)

	files_search_var($1)
	admin_pattern($1, pegasus_cache_t)

	files_search_var_lib($1)
	admin_pattern($1, pegasus_data_t)

	files_search_pids($1)
	admin_pattern($1, pegasus_var_run_t)
')
