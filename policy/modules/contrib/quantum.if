## <summary>Virtual network service for Openstack.</summary>

########################################
## <summary>
##	All of the rules required to
##	administrate an quantum environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`quantum_admin',`
	gen_require(`
		type quantum_t, quantum_initrc_exec_t, quantum_log_t;
		type quantum_var_lib_t, quantum_tmp_t;
	')

	allow $1 quantum_t:process { ptrace signal_perms };
	ps_process_pattern($1, quantum_t)

	init_labeled_script_domtrans($1, quantum_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 quantum_initrc_exec_t system_r;
	allow $2 system_r;

	logging_search_logs($1)
	admin_pattern($1, quantum_log_t)

	files_search_var_lib($1)
	admin_pattern($1, quantum_var_lib_t)

	files_search_tmp($1)
	admin_pattern($1, quantum_tmp_t)
')
