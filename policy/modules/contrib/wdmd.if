## <summary>Watchdog multiplexing daemon.</summary>

########################################
## <summary>
##	Connect to wdmd with a unix
##	domain stream socket.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`wdmd_stream_connect',`
	gen_require(`
		type wdmd_t, wdmd_var_run_t;
	')

	files_search_pids($1)
	stream_connect_pattern($1, wdmd_var_run_t, wdmd_var_run_t, wdmd_t)
')

########################################
## <summary>
##	All of the rules required to
##	administrate an wdmd environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`wdmd_admin',`
	gen_require(`
		type wdmd_t, wdmd_initrc_exec_t, wdmd_var_run_t;
	')

	allow $1 wdmd_t:process { ptrace signal_perms };
	ps_process_pattern($1, wdmd_t)

	init_labeled_script_domtrans($1, wdmd_initrc_exec_t)
	domain_system_change_exemption($1)
	role_transition $2 wdmd_initrc_exec_t system_r;
	allow $2 system_r;

	files_search_pids($1)
	admin_pattern($1, wdmd_var_run_t)
')
