## <summary>Console network traffic monitor.</summary>

########################################
## <summary>
##	Execute a domain transition to run vnstat.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`vnstatd_domtrans_vnstat',`
	gen_require(`
		type vnstat_t, vnstat_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, vnstat_exec_t, vnstat_t)
')

########################################
## <summary>
##	Execute vnstat in the vnstat domain,
##	and allow the specified role
##	the vnstat domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
#
interface(`vnstatd_run_vnstat',`
	gen_require(`
		attribute_role vnstat_roles;
	')

	vnstatd_domtrans_vnstat($1)
	roleattribute $2 vnstat_roles;
')

########################################
## <summary>
##	All of the rules required to
##	administrate an vnstatd environment.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`vnstatd_admin',`
	gen_require(`
		type vnstatd_t, vnstatd_initrc_exec_t;
		type vnstatd_pid_t, vnstatd_unit_t, vnstatd_var_lib_t;
	')

	admin_process_pattern($1, vnstatd_t)

	init_startstop_service($1, $2, vnstatd_t, vnstatd_initrc_exec_t, vnstatd_unit_t)

	files_search_runtime($1)
	admin_pattern($1, vnstatd_pid_t)

	files_list_var_lib($1)
	admin_pattern($1, vnstatd_var_lib_t)

	vnstatd_run_vnstat($1, $2)
')
